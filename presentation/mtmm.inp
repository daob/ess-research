TITLE:  mtmm analysis of generated data

DATA:
  FILE IS "C:\data\trust-GB.dat";
  
  !FILE IS "C:\ESS\berlin\presentation\testdata.dat";
  
    

VARIABLE:
  NAMES ARE 
 TRSTPRL TRSTLGL TRSTPLT TESTA25 TESTA26 TESTA27 TESTA38 TESTA39 TESTA40;
  MISSING ARE ALL(-999);
    

ANALYSIS:
  TYPE IS MISSING;
   ESTIMATOR = ML;


MODEL:
  F1 BY TESTA25@1 TESTA38 TRSTPRL; ! errors in variables standardisation
  F2 BY TESTA26@1 TESTA39 TRSTLGL ;
  F3 BY TESTA27@1 TESTA40 TRSTPLT ;

  F1 WITH F2 F3; ! correlated trait
  F2 WITH F3;

  F1 F2 F3 M1 M2 M3; ! trait, method var's
  TRSTPRL TRSTLGL TRSTPLT TESTA25 TESTA26 TESTA27 TESTA38 TESTA39 TESTA40; ! error variances


  M1 BY TRSTPRL@1 TRSTLGL@1 TRSTPLT@1;  ! equal method effects
  M2 BY TESTA25@1 TESTA26@1 TESTA27@1;
  M3 BY TESTA38@1 TESTA39@1 TESTA40@1;

  M1 WITH F1@0 F2@0 F3@0 M2@0 M3@0; ! uncorrelated traits<>method, methods.
  M2 WITH F1@0 F2@0 F3@0 M3@0;
  M3 WITH F1@0 F2@0 F3@0 M2@0;

[TRSTPRL TRSTLGL TRSTPLT TESTA25 TESTA26 TESTA27 TESTA38 TESTA39 TESTA40];
[F1@0 F2@0 F3@0 M1@0 M2@0 M3@0];
OUTPUT:  SAMPSTAT MODINDICES(0) RESIDUAL STANDARDIZED CINTERVAL 
 TECH1 TECH2 TECH3 TECH4 TECH5;


!SAVEDATA:
 !   SAVE = FSCORES;
    !FILE IS "C:\ESS\berlin\presentation\facscores.dat";