TITLE: MTMM analysis women-small model
DATA: 
 FILE IS ..\..\data\women.dat;

VARIABLE: 
 NAMES ARE 
	ID	cntrynum		dweight 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	TEST22 TEST23 TEST24
	SPLTADMA;

 USEVARIABLES ARE 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	;

 USEOBSERVATIONS ARE 
   (dweight NE -9999 AND SPLTADMA EQ 1 AND
	 cntrynum EQ 3 OR cntrynum EQ 22 OR
	cntrynum EQ  4 OR cntrynum EQ 12 );
 CATEGORICAL ARE 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	;

 WEIGHT IS dweight;
 GROUPING IS cntrynum (3=CH 4=CZ 12=GR 22=SI);
 IDVARIABLE = ID;
 MISSING ARE ALL(-9999);


ANALYSIS: 
 TYPE = MISSING MEANSTRUCTURE;
 ESTIMATOR IS WLSMV;
 PROCESSORS=2;
 PARAMETERIZATION =THETA;
 ITERATIONS = 1000;
 SDITERATIONS = 30;
 CONVERGENCE = 0.005;



MODEL: 

     T1 BY WORK@1;
     T2 BY MENFST@1;
     T3 BY MENJB@1;
     T1 BY TEST8*-.775 ;
     T2 BY TEST9*-1.305 ;
     T3 BY TEST10*-.763 ;
     
     [M1-M2@0];

     M1 BY WORK@1 MENFST@1 MENJB@1;
     M2 BY TEST8@1 TEST9@1 TEST10@1;

     T1*1
     T2*1
     T3*1  ;
     M1    
     M2     ;

     T1 WITH T2  ;
     T1 WITH T3  ;
     T2 WITH T3  ;

     T1 WITH M1@0;
     T2 WITH M1@0;
     T3 WITH M1@0;

     T1 WITH M2@0;
     T2 WITH M2@0;
     T3 WITH M2@0;

     M1 WITH M2@0;

	[TEST10$1 TEST10$2 TEST10$3 TEST10$4 ];
	[TEST9$1 TEST9$2 TEST9$3 TEST9$4 ];
	[TEST8$1 TEST8$2 TEST8$3 TEST8$4 ];
	[MENJB$1 MENJB$2 MENJB$3 MENJB$4 ];
	[MENFST$1 MENFST$2 MENFST$3 MENFST$4 ];
	[WORK$1 WORK$2 WORK$3 WORK$4 ];

     WORK ;
     MENFST ;
     MENJB ;
     TEST8  ;
     TEST9  ;

MODEL CH: 
  [T1-T3@0];
   M1 M2;
     T1 BY TEST8*-.870 ;
     T2 BY TEST9*-1.334;
     T3 BY TEST10*-1.015 ;
     [ WORK$1 WORK$4 MENFST$3 MENJB$1 MENJB$2 
      MENJB$4 TEST8$2 TEST8$4 TEST9$1 TEST9$2
      TEST10$1 TEST10$3 ];
MODEL CZ: 
     M1 M2;
     T1 BY TEST8*-1.528 ;
     T2 BY TEST9*-1.334;
     T3 BY TEST10*-.628 ;
   [ WORK$1   WORK$2   WORK$3  
     WORK$4   MENFST$1 MENFST$3 MENFST$4 MENJB$1 
     MENJB$3  TEST8$1  TEST8$3  TEST9$2  TEST9$3 
     TEST10$1 TEST10$3 ];
MODEL GR:
   M1@0 M2;
     T1 BY TEST8*-.660 ;
     T2 BY TEST9*-1.334;
     T3 BY TEST10*-.735 ;
   [ WORK$1   MENFST$1 MENJB$1 
     MENJB$2  MENJB$4  TEST8$2  TEST8$3  TEST8$4 
     TEST9$3  TEST10$2 ];
MODEL SI:
   M1 M2;
     T1 BY TEST8*-.602 ;
     T2 BY TEST9*-1.334;
     T3 BY TEST10*-.644 ;
   [ MENFST$2 MENFST$3 MENFST$4
     MENJB$1  MENJB$2  MENJB$4  TEST9$1  TEST9$3 
     TEST9$4  TEST10$1 TEST10$2 TEST10$4 ];
OUTPUT:
      SAMPSTAT MODINDICES(0) RESIDUAL STANDARDIZED CINTERVAL
   PATTERNS TECH1 TECH2 TECH3 TECH4 TECH5
	TECH6 TECH7 TECH8 TECH9 TECH10 ;

SAVEDATA:
	FILE IS save-women.dat;
	RESULTS ARE results-women.dat;
	ESTIMATES ARE estimates-women.dat;
	TECH4 IS covariances-women.dat;
	FORMAT IS free;

PLOT:
   TYPE=PLOT3 PLOT2 PLOT1;
