TITLE: MTMM analysis women-small model
DATA: 
 FILE IS ..\..\data\women.dat;

VARIABLE: 
 NAMES ARE 
	ID	cntrynum		dweight 
	WMCPWRK MNRSPHM MNRGTJB 
	TESTA8 TESTA9 TESTA10 
	TESTA22 TESTA23 TESTA24
	SPLTADMA;

 USEVARIABLES ARE 
	WMCPWRK MNRSPHM MNRGTJB 
	TESTA8 TESTA9 TESTA10 
	;

 USEOBSERVATIONS ARE 
   (dweight NE -9999 AND SPLTADMA EQ 1 AND
	 cntrynum EQ 3 OR cntrynum EQ 22 OR
	cntrynum EQ  4 OR cntrynum EQ 12 );
 CATEGORICAL ARE 
	WMCPWRK MNRSPHM MNRGTJB 
	TESTA8 TESTA9 TESTA10 
	;

 WEIGHT IS dweight;
 GROUPING IS cntrynum (3=CH 4=CZ 12=GR 22=SI);
 IDVARIABLE = ID;
 MISSING ARE ALL(-9999);


ANALYSIS: 
 TYPE = MISSING MEANSTRUCTURE;
 ESTIMATOR IS WLS;
 PROCESSORS=2;
 PARAMETERIZATION =THETA;
 ITERATIONS = 1000;
 SDITERATIONS = 30;
 CONVERGENCE = 0.005;



MODEL: 

     T1 BY WMCPWRK@1;
     T2 BY MNRSPHM@1;
     T3 BY MNRGTJB@1;
     T1 BY TESTA8 ;
     T2 BY TESTA9 ;
     T3 BY TESTA10 ;
     
     [M1-M2@0];

     M1 BY WMCPWRK@1 MNRSPHM@1 MNRGTJB@1;
     M2 BY TESTA8@1 TESTA9@1 TESTA10@1;

     T1*1
     T2*1
     T3*1  ;
     M1    
     M2     ;

     T1 WITH T2  ;
     T1 WITH T3  ;
     T2 WITH T3  ;

     T1 WITH M1@0;
     T2 WITH M1@0;
     T3 WITH M1@0;

     T1 WITH M2@0;
     T2 WITH M2@0;
     T3 WITH M2@0;

     M1 WITH M2@0;

	[TESTA10$1 TESTA10$2 TESTA10$3 TESTA10$4 ];
	[TESTA9$1 TESTA9$2 TESTA9$3 TESTA9$4 ];
	[TESTA8$1 TESTA8$2 TESTA8$3 TESTA8$4 ];
	[MNRGTJB$1 MNRGTJB$2 MNRGTJB$3 MNRGTJB$4 ];
	[MNRSPHM$1 MNRSPHM$2 MNRSPHM$3 MNRSPHM$4 ];
	[WMCPWRK$1 WMCPWRK$2 WMCPWRK$3 WMCPWRK$4 ];

     WMCPWRK ;
     MNRSPHM ;
     MNRGTJB ;
     TESTA8  ;
     TESTA9  ;

MODEL CH: 
    [T1-T3@0];
    M1 M2;
MODEL CZ: 
M1 M2;
MODEL GR:
M1 M2;
MODEL SI:
M1 M2;
  OUTPUT:
      SAMPSTAT MODINDICES(10) RESIDUAL STANDARDIZED CINTERVAL
   PATTERNS TECH1 TECH2 TECH3 TECH4 TECH5
	TECH6 TECH7 TECH8 TECH9 TECH10 ;

SAVEDATA:
	FILE IS save-women.dat;
	RESULTS ARE results-women.dat;
	ESTIMATES ARE estimates-women.dat;
	TECH4 IS covariances-women.dat;
	FORMAT IS free;

PLOT:
   TYPE=PLOT3 PLOT2 PLOT1;
