TITLE: MTMM analysis women-small model
DATA: 
 FILE IS C:\ESS\berlin\data\women.dat;

VARIABLE: 
 NAMES ARE 
	ID	cntrynum		dweight 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	TEST22 TEST23 TEST24
	SPLTADMA;

 USEVARIABLES ARE 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	;

 USEOBSERVATIONS ARE 
   (dweight NE -9999 AND SPLTADMA EQ 1 AND
	 cntrynum EQ 3 OR cntrynum EQ 22 OR
	cntrynum EQ  4 OR cntrynum EQ 12 );
 CATEGORICAL ARE 
	WORK MENFST MENJB 
	TEST8 TEST9 TEST10 
	;

 WEIGHT IS dweight;
 GROUPING IS cntrynum (3=CH 4=CZ 12=GR 22=SI);
 IDVARIABLE = ID;
 MISSING ARE ALL(-9999);


ANALYSIS: 
 TYPE = MISSING MEANSTRUCTURE;
 ESTIMATOR IS WLS;
 PROCESSORS=2;
 PARAMETERIZATION =THETA;


MODEL: 
! Categorization part of the model:
     L_WORK BY WORK@1;
     L_MENFST BY MENFST@1;
     L_MENJB BY MENJB@1;
     L_TEST8 BY TEST8@1;
     L_TEST9 BY TEST9@1;
     L_TEST10 BY TEST10@1; 

	[TEST10$1 TEST10$2 TEST10$3 TEST10$4 ];
	[TEST9$1 TEST9$2 TEST9$3 TEST9$4 ];
	[TEST8$1 TEST8$2 TEST8$3 TEST8$4 ];
	[MENJB$1 MENJB$2 MENJB$3 MENJB$4 ];
	[MENFST$1 MENFST$2 MENFST$3 MENFST$4 ];
	[WORK$1 WORK$2 WORK$3 WORK$4 ];

     [ L_WORK L_MENFST L_MENJB L_TEST8 L_TEST9 L_TEST10 ];
     WORK@0 MENFST@0 MENJB@0 TEST8@0 TEST9@0 TEST10@0 ;

    L_WORK WITH L_MENFST@0 L_MENJB@0 L_TEST8@0 L_TEST9@0 L_TEST10@0;
    L_MENFST WITH  L_MENJB@0 L_TEST8@0 L_TEST9@0 L_TEST10@0;
    L_MENJB WITH  L_TEST8@0 L_TEST9@0 L_TEST10@0;
    L_TEST8 WITH   L_TEST9@0 L_TEST10@0;
    L_TEST9 WITH    L_TEST10@0;

! Continuous part of the model:
    TWork BY L_WORK@1;
    TMenFt BY L_MENFST@1;
    TMenJb BY L_MENJB@1;

    MWMin BY L_WORK@1 L_MENFST@1 L_MENJB@1;
    MWPlus BY L_TEST8@1 L_TEST9@1 L_TEST10@1;

    TWork BY L_TEST8;
    TMenFt BY L_TEST9;
    TMenJb BY L_TEST10;

    TWork TMenFt TMenJb  MWMin MWPlus;

! Residual error variances:
     L_WORK L_MENFST L_MENJB 
     L_TEST8 L_TEST9 L_TEST10;

    TWork WITH TMenFt TMenJb  MWMin@0 MWPlus@0;
    TMenFt WITH TMenJb  MWMin@0 MWPlus@0;
    TMenJb  WITH MWMin@0 MWPlus@0;
    MWMin WITH MWPlus@0;

MODEL CH: 
     [ L_WORK@0 L_MENFST@0 L_MENJB@0 L_TEST8@0 L_TEST9@0 L_TEST10@0 ];
     
     L_WORK@1 L_MENFST@1 L_MENJB@1 L_TEST8@1 L_TEST9@1 L_TEST10@1;
     
     WORK@0 MENFST@0 MENJB@0 TEST8@0 TEST9@0 TEST10@0;

     L_WORK L_MENFST L_MENJB 
     L_TEST8 L_TEST9 L_TEST10;

    TWork BY L_TEST8;
    TMenFt BY L_TEST9;
    TMenJb BY L_TEST10;

    TWork TMenFt TMenJb  MWMin MWPlus;
   [ 
    WORK$1 WORK$4      
     MENJB$1 
     TEST8$3 TEST8$4 
   ];


MODEL CZ: 
 [ 
    WORK$4 
    MENJB$1  MENJB$3 
    TEST8$3  TEST8$4 
    TEST9$3 TEST9$4 
    TEST10$1 TEST10$2 
 ];
 
    TWork BY L_TEST8;
    TMenFt BY L_TEST9;
    TMenJb BY L_TEST10;

     L_WORK L_MENFST L_MENJB 
     L_TEST8 L_TEST9 L_TEST10;

    TWork TMenFt TMenJb  MWMin MWPlus;

MODEL GR:
  [ 
    WORK$4              (11) !WORK$1
    MENFST$1  
    MENFST$3
    MENJB$1             (12)
        !MENJB$4
    TEST8$2 TEST8$3 
    TEST9$3 TEST9$4 
    TEST10$1 TEST10$2 
  ];
   

    TWork BY L_TEST8;
    TMenFt BY L_TEST9;
    TMenJb BY L_TEST10;

     L_WORK L_MENFST L_MENJB 
     L_TEST8 L_TEST9 L_TEST10;

    TWork TMenFt TMenJb  MWMin MWPlus;

MODEL SI:
 [ 
    MENFST$1 MENFST$2
    MENJB$1             (12)
        MENJB$4 
    TEST9$4 
    TEST10$1 TEST10$4 
    WORK$2
        WORK$4          (11)
 ];

    TWork BY L_TEST8;
    TMenFt BY L_TEST9;
    TMenJb BY L_TEST10;

    TWork TMenFt TMenJb  MWMin MWPlus;

OUTPUT:
      SAMPSTAT MODINDICES(0) RESIDUAL STANDARDIZED CINTERVAL TECH1 TECH2 TECH3;

PLOT:
   TYPE =  PLOT3 PLOT2 PLOT1;

